model {
  linf ~ dunif(100, 800)
  k ~ dunif(0, 0.5)
  t0 ~ dunif(-1.25, 0.5)
  sig ~ dunif(0, 2)
  linf_diff ~ dunif(-500, 500)
  for (i in 1:n_obs) {
    length[i] ~ dlnorm(log(length_hat[i]), 1/sig^2)
    length_hat[i] <- (linf + linf_diff * basin[i]) * (1 - exp(-k * (age[i] - t0)))
  }
  for (i in 1:n_pred) {
    pred_length[i, 1] <- linf * (1 - exp(-k * (pred_age[i] - t0)))
    pred_length[i, 2] <- (linf + linf_diff) * (1 - exp(-k * (pred_age[i] - t0)))
    pred_length[i, 3] <- (linf - linf_diff) * (1 - exp(-k * (pred_age[i] - t0)))
    epi[i] ~ dlnorm(0, 1/sig^2)
    rand_length[i, 1] <- epi[i] * pred_length[i, 1]
    rand_length[i, 2] <- epi[i] * pred_length[i, 2]
    rand_length[i, 3] <- epi[i] * pred_length[i, 3]
  }
}
